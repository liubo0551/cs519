#version 330 compatibility
#extension GL_EXT_gpu_shader4: enable
#extension GL_EXT_geometry_shader4: enable

layout( triangles )  in;
layout( line_strip, max_vertices=200 )  out;

uniform int	uDetail;
uniform float	uDroop;
uniform int	uLength;
uniform float	uStep;

in vec3  vTnorm[3];
in vec4  vColor[3];
out vec4 gColor;

int ILength;
vec3 Norm[3];
vec3 N0, N01, N02;
vec4 V0, V01, V02;

void
ProduceVertices( float s, float t )
{
	vec4 v = V0 + s*V01 + t*V02;
	vec3 n = normalize( N0 + s*N01 + t*N02 );

	for( int i = 0; i <= uLength; i++ )
	{
		gl_Position = gl_ProjectionMatrix * v;
		gColor = vColor[0];
		EmitVertex( );
		v.xyz += uStep * n;
		v.y -= uDroop * float(i*i);
	}
	EndPrimitive( );
}



void
main( )
{
	V0  =   gl_PositionIn[0];
	V01 = ( gl_PositionIn[1] - gl_PositionIn[0] );
	V02 = ( gl_PositionIn[2] - gl_PositionIn[0] );

	Norm[0] = vTnorm[0];
	Norm[1] = vTnorm[1];
	Norm[2] = vTnorm[2];

	if( dot( Norm[0], Norm[1] ) < 0. )
		Norm[1] = -Norm[1];

	if( dot( Norm[0], Norm[2] ) < 0. )
		Norm[2] = -Norm[2];

	N0  = Norm[0];
	N01 = Norm[1] - Norm[0];
	N02 = Norm[2] - Norm[0];

	int numLayers = 1 << uDetail;

	float dt = 1. / float( numLayers );
	float t = 1.;

	for( int it = 0; it <= numLayers; it++ )
	{
		float smax = 1. - t;

		int nums = it + 1;
		float ds = smax / float( nums - 1 );

		float s = 0.;
		for( int is = 0; is < nums; is++ )
		{
			ProduceVertices( s, t );
			s += ds;
		}

		t -= dt;
	}
}
